include: output.def
include: macros_www/html.def
# include: macros_www/php.def
# include: macros_www/field.def

page: output_www, output_main
    type: pm
    output_dir: lib/MyDef
    ext: html
    init_mode: html
    package: MyDef::output_www

subcode:: package_globals
    use Term::ANSIColor qw(:constants);

subcode:: init_page
    $global $style, @style_key_list
    $style={};
    @style_key_list=()

    $global $style_sheets
    $style_sheets=[]

subcode: modeswitch
    # stackable mode : html -> js -> php
    $global @mode_stack
    $global $cur_mode="html"

    # print "modeswitch $cur_mode-> $mode\n"

    $if $mode eq $cur_mode or $mode eq "sub"
        goto modeswitch_done

    $if $cur_mode eq "php"
        $call exit_php
        $call popmode

    $if $mode eq "php"
        $call enter_php
        $call pushmode

    $if $cur_mode eq "js"
        $call exit_js
        $call popmode

    $if $mode eq "js"
        $call enter_js
        $call pushmode

    modeswitch_done:

    # --
    subcode: enter_js
        MyDef::compileutil::push_interface("js")
        push @$out, "<script type=\"text/javascript\">\n";

    subcode: exit_js
        push @$out, "<\/script>\n"
        MyDef::compileutil::pop_interface()

    # --
    subcode: enter_php
        MyDef::compileutil::push_interface("php")
        push @$out, "<?php\n"

    subcode: exit_php
        push @$out, "?>\n";
        MyDef::compileutil::pop_interface()

    # --
    subcode: pushmode
        push @mode_stack, $cur_mode
        $cur_mode=$mode
        goto modeswitch_done

    subcode: popmode
        $cur_mode=pop @mode_stack
        $if $mode eq $cur_mode
            goto modeswitch_done
# ------------------------
subcode:: dumpout

subcode: parsecode
    $call parsecode_common

subcode: parsecode_special
    $case $l=~/^CSS:\s*(.*)/
        my $t=$1
        $if $t=~/(.*?)\s*\{(.*)\}/
            $if $style->{$1}
                $style->{$1}.=";$2";
            $else
                $style->{$1}=$2;
                push @style_key_list, $1;
        $elif $t=~/(\S*\.css)/
            push @$style_sheets, $1
        return

subcode: parsecode_func_param

subcode: parsecode_func
    $call parsecode_html_tag
    $if $func eq "script"
        MyDef::compileutil::modepush("js")
        &call return_newblock, script
            $call push_indented_block
            push @src, "PARSE:MODEPOP"
    $elif $func=~/^(title|charset)/
        $page->{$1}=$2
        return
    $elif $func eq "include"
        $call include_html_template

subcode: parsecode_other
    $if $page->{type} eq "php"
        $l=~s/(\$\w+)/<?php echo $1 ?>/g

